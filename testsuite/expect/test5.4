#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Check squeue formating options (--noheader, --format and --step 
#          options and SQUEUE_FORMAT environment variable).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2002 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# UCRL-CODE-217948.
# 
# This file is part of SLURM, a resource management program.
# For details, see <http://www.llnl.gov/linux/slurm/>.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA.
############################################################################
source ./globals

set test_id      "5.4"
set exit_code    0
set file_in      "test$test_id.input"
set job_id1      0
set job_id2      0

print_header $test_id
#
# Delete left-over input script
# Build input script file
#
exec $bin_rm -f $file_in
exec echo "#!$bin_bash"          >$file_in
exec echo "$srun $bin_sleep 90" >>$file_in
exec $bin_chmod 700 $file_in

#
# Submit a couple of job so we have something to look at
#
spawn $srun --batch --output=/dev/null --error=/dev/null -t1 $file_in
expect {
	-re "jobid ($number) submitted" {
		set job_id1 $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		exit 1
	}
	eof {
		wait
	}
}
if {$job_id1 == 0} {
	send_user "\nFAILURE: srun job submit failure\n"
	kill_srun
	exit 1
}

spawn $srun --batch --output=/dev/null --error=/dev/null --hold -t1 $file_in
expect {
	-re "jobid ($number) submitted" {
		set job_id2 $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		exit 1
	}
	eof {
		wait
	}
}
if {$job_id2 == 0} {
        send_user "\nFAILURE: srun job submit failure\n"
	kill_srun
	exit 1
}

if {[wait_for_job $job_id1 RUNNING] != 0} {
	send_user "\nFAILURE to start job $job_id1\n"
	kill_srun
	exit 1
}
exec $bin_rm -f $file_in

#
# Check the squeue noheader output
#

set have_header 0
spawn $squeue --noheader
expect {
	-re "PARTITION" {
		set have_header 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: squeue not responding\n"
		cancel_job $job_id2
		set exit_code 1
	}
	eof {
		wait
	}
}

if {$have_header > 0} {
	send_user "\nFAILURE: squeue noheader error\n"
	set exit_code 1
}

#
# Check the squeue format output via command line option
#

# For some reason, quoting the format value breaks expect, 
#  but this passes the entire format in a single argv entry
set format "--format=jid=%i uid=%U"
set have_job_ids 0
spawn $squeue $format
expect {
	-re "($end_of_line)jid=($number) uid=($number)" {
		if {$expect_out(2,string) == $job_id1} {
			incr have_job_ids
		}
		if {$expect_out(2,string) == $job_id2} {
			incr have_job_ids
		}
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: squeue not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}

if {$have_job_ids == 0} {
	send_user "\nFAILURE: squeue format error\n"
	set exit_code 1
}
if {$have_job_ids == 1} {
	send_user "\nFAILURE: squeue found only 1 of 2 jobs\n"
	set exit_code 1
}

#
# Check the squeue format output via SQUEUE_FORMAT environment 
# variable
#

set env(SQUEUE_FORMAT) "JID=%i UID=%U"
set have_job_ids 0
spawn $squeue
expect {
	-re "($end_of_line)JID=($number) UID=($number)" {
		if {$expect_out(2,string) == $job_id1} {
			incr have_job_ids
		}
		if {$expect_out(2,string) == $job_id2} {
			incr have_job_ids
		}
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: squeue not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}
unset env(SQUEUE_FORMAT)

if {$have_job_ids == 0} {
	send_user "\nFAILURE: squeue format error\n"
	set exit_code 1
}
if {$have_job_ids == 1} {
	send_user "\nFAILURE: squeue found only 1 of 2 jobs\n"
	set exit_code 1
}

#
# Wait for the job step to appear
#

spawn $squeue --iterate=5 --steps
set step_id    "$job_id1.0"
set iteration  0
set step_found 0
expect {
	-re "($end_of_line)($job_id1.0) " {
		set step_found 1
		set squeue_pid [exp_pid]
		exec $bin_kill -TERM $squeue_pid
		exp_continue
	}
	-re "PARTITION" {
		incr iteration
		if {$iteration >= 3} {
			set squeue_pid [exp_pid]
			exec $bin_kill -TERM $squeue_pid
		}
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: squeue not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}

if {$step_found == 0} {
	send_user "\nFAILURE: squeue step format error\n"
	set exit_code 1
}

cancel_job $job_id1
cancel_job $job_id2
if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code

