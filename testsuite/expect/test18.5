#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Confirm that slauch local stdin, stdout, and stderr options work 
#          (options --slaunch-input, --slaunch-output and --slaunch-error 
#          respectively).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2002 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# UCRL-CODE-226842.
# 
# This file is part of SLURM, a resource management program.
# For details, see <http://www.llnl.gov/linux/slurm/>.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA.
############################################################################
source ./globals

set test_id     "18.5"
set file_in     "test$test_id.input"
set file_out    "test$test_id.output"
set file_err    "test$test_id.error"

set exit_code            0
set login_grp_info       ""
set job_grp_info         ""
set got_job_grps         0
set got_login_grps       0
set got_sleep_err        0

print_header $test_id

#
# Delete left-over stdin/out/err files
# Build stdin file
#
exec $bin_rm -f $file_in $file_out $file_err
exec echo "$bin_id"         >$file_in
exec echo "$bin_sleep aaa" >>$file_in
exec echo "exit 0"         >>$file_in
exec $bin_chmod 700 $file_in

#
# Spawn a shell via slaunch that uses stdin/out/err and confirm their contents
#
set job_id  0
set timeout $max_job_delay
set salloc_pid [spawn $salloc -N1 -t1 $slaunch --slaunch-input=$file_in --slaunch-output=$file_out --slaunch-error=$file_err $bin_bash]
expect {
	-re "Granted job allocation ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	-re "Unable to contact" {
		send_user "\nFAILURE: slurm appears to be down\n"
		exit 1
	}
	timeout {
		send_user "\nFAILURE: salloc/slaunch not responding\n"
		if {$job_id != 0} {
			cancel_job $job_id
		}
		slow_kill [expr 0 - $salloc_pid]
		set exit_code 1
	}
	eof {
		wait
	}
}

#
# Check user id and group id in stdout
#
spawn $bin_id
expect {
	-re "(uid=.*\n)" {
		set login_grp_info $expect_out(1,string)
		set got_login_grps 1
		exp_continue
	}
	eof {
		wait
	}
}

if {[wait_for_file $file_out] == 0} {
	spawn $bin_cat $file_out
	expect {
		-re "(uid=.*\n)" {
			set job_grp_info $expect_out(1,string)
			set got_job_grps 1
			exp_continue
		}
		eof {
			wait
		}
	}
}

if {$got_login_grps == 0} {
	send_user "\nFAILURE: Unable to get user and group ID info\n"
	set exit_code 1
}
if {$got_job_grps == 0} {
	send_user "\nFAILURE: User and group ID info missing from stdout\n"
	set exit_code 1
}
if {[string compare $login_grp_info $job_grp_info] != 0} {
	send_user "\nFAILURE: Login and slurm user info mismatch\n"
	set exit_code 1
}

#
# Check for sleep input specification error in stderr
#
if {[wait_for_file $file_err] == 0} {
	spawn $bin_cat $file_err
	expect {
		-re "$sleep_error_message" {
			send_user "\nNo worries, this error is expected\n"
			set got_sleep_err 1
			exp_continue
		}
		-re "Specify time as a positive integer.*\n" {
			set got_sleep_err 1
			exp_continue
		}
		eof {
			wait
		}
	}
}
if {$got_sleep_err == 0} {
	send_user "\nFAILURE: Unexpected stderr contents\n"
	set exit_code 1
}

#
# Spawn a program to run for a while with no input, output or error
#
set job_id 0
set salloc_pid [spawn $salloc -N1 -t1 $slaunch --slaunch-input=none --slaunch-output=none --slaunch-error=none $bin_od -c $srun]
expect {
	-re "Granted job allocation ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	-re "Unable to contact" {
		send_user "\nFAILURE: slurm appears to be down\n"
		exit 1
	}
	-re "Terminated" {
		send_user "\nFAILURE: srun failed to complete\n"
		set exit_code 1
		exp_continue
	}
	-re "job exceeded timelimit" {
		send_user "\nFAILURE: srun failed to complete\n"
		set exit_code 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: salloc not responding\n"
		if {$job_id != 0} {
			cancel_job $job_id
		}
		slow_kill [expr 0 - $salloc_pid]
		set exit_code 1
	}
	eof {
		wait
	}
}

if {$exit_code == 0} {
	exec $bin_rm -f $file_in $file_out $file_err
	send_user "\nSUCCESS\n"
}
exit $exit_code
