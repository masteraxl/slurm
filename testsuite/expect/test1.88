#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Basic test of MPI functionality
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "WARNING: ..." with an explanation of why the test can't be made, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
#
# Note:    This script generates and then deletes a file in the working 
#          directory named test1.58.prog
############################################################################
# Copyright (C) 2002 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# UCRL-CODE-2002-040.
# 
# This file is part of SLURM, a resource management program.
# For details, see <http://www.llnl.gov/linux/slurm/>.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA.
############################################################################
source ./globals

set test_id            "1.88"
set exit_code          0
set file_in            "test$test_id.input"
set file_out           "test$test_id.output"
set file_err           "test$test_id.error"
set test_prog          "test$test_id.prog"
set job_id             0

print_header $test_id

#
# Test for existence of mpi compiler
#
if {[info exists mpicc] == 0} {
	send_user "\nWARNING: mpicc not defined, can't perform mpi testing\n"
	exit 0
}
if {[file executable $mpicc] == 0} {
	send_user "\nWARNING: $mpicc does not exists\n"
	exit 0
}
if {[test_front_end] != 0} {
        send_user "\nWARNING: This test is incompatable with front-end systems\n"
        exit 0
}

#
# Delete left-over program and rebuild it
#
exec $bin_rm -f $test_prog ${test_prog}.o
exec $mpicc -o $test_prog ${test_prog}.c

#
# Delete left-over input script plus stdout/err files
# Build input script file
#
exec $bin_rm -f $file_in $file_out $file_err
exec echo "#!$bin_bash"							 >$file_in
exec echo "$bin_echo test1_N3_n6_cyclic"				>>$file_in
exec echo "$srun -N3 -n6 --distribution=cyclic $test_prog"		>>$file_in
exec echo "$bin_echo test2_N3_n6_block"					>>$file_in
exec echo "$srun -N3 -n6 --distribution=block  $test_prog"		>>$file_in
exec echo "$bin_echo test3_N3_n4_cyclic"				>>$file_in
exec echo "$srun -N3 -n4 --distribution=cyclic $test_prog"		>>$file_in
exec echo "$bin_echo test4_N3_n4_block"					>>$file_in
exec echo "$srun -N3 -n4 --distribution=block  $test_prog"		>>$file_in
exec echo "$bin_echo test5_N2_n4_with_hole"				>>$file_in
exec echo "$srun -N2 -n4 --distribution=cyclic --relative=\[0,2\]  $test_prog" >>$file_in
exec echo "$bin_echo TEST_COMPLETE"					>>$file_in
exec $bin_chmod 700 $file_in

#
# Spawn a srun batch job that uses stdout/err and confirm their contents
#
set timeout $max_job_delay
set no_start 0
spawn $srun -N3 --batch --output=$file_out --error=$file_err -t1 $file_in
expect {
	-re "jobid ($number) submitted" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	-re "configuration not available" {
		set no_start 1
		exp_continue
	}
	-re "Unable to submit batch job" {
		set no_start 1
		exp_continue
	}
	-re "Unable to contact" {
		send_user "\nFAILURE: slurm appears to be down\n"
		exit 1
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
		exp_continue
	}
	eof {
		wait
	}
}

if {$no_start != 0} {
	send_user "\nWARNING: partition too small for test\n"
	if {$job_id != 0} {
		cancel_job $job_id
	}
	exit 0
}
if {$job_id == 0} {
	send_user "\nFAILURE: batch submit failure\n"
	exit 1
}

#
# Wait for job to complete
#
if {[wait_for_job $job_id "DONE"] != 0} {
	send_user "\nFAILURE: waiting for job to complete\n"
	set exit_code 1
}

#
# Check for desired output in stdout
#
if {[wait_for_file $file_out] == 0} {
	set matches  0
	set complete 0
	spawn $bin_cat $file_out
	expect {
                -re "I just received msg from Rank" {
			incr matches
                        exp_continue
                }
		-re "TEST_COMPLETE" {
			incr complete
			exp_continue
		}
                eof {
			wait
                }
        }
	if {$matches != [expr 6 + 6 + 4 + 4 + 4]} {
		send_user "\nFAILURE: unexpected output\n"
		set exit_code 1
	} elseif {$complete == 0} {
		send_user "\nFAILURE: test failed to complete\n"
		set exit_code 1
	}
} else {
	set exit_code 1
}

if {$exit_code == 0} {
	exec $bin_rm -f $file_in $file_out $file_err $test_prog ${test_prog}.o
	send_user "\nSUCCESS\n"
}
exit $exit_code
